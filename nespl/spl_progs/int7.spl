alias currentPID R15;
currentPID = [SYSTEM_STATUS_TABLE + 5*CORE + 1];
[PROCESS_TABLE + currentPID*16 + 9] = INT_WRITE;

alias userSP R5;
userSP = SP;

[PROCESS_TABLE + currentPID*16 + 13] = SP;
SP = [PROCESS_TABLE + currentPID*16 + 11]*512 - 1;

alias fileDescriptor R6;
fileDescriptor = [[PTBR + 2*(userSP - 4)/512]*512 + (userSP - 4)%512];

alias physicalAddrRetVal R14;
physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;

backup;
R1 = ACQUIRE_KERN_LOCK;
call ACCESS_CONTROL;
restore;

if (fileDescriptor != -2) then
  alias userAreaPageNum R7;
  userAreaPageNum = [PROCESS_TABLE + currentPID*16 + 11];

  if (fileDescriptor < 0 || fileDescriptor >= 8 || [userAreaPageNum*512 + 496 + 2*fileDescriptor] != FILE) then
    [physicalAddrRetVal] = -1;

    backup;
    R1 = RELEASE_LOCK;
    R2 = KERN_LOCK;
    call ACCESS_CONTROL;
    restore;

    SP = userSP;
    [PROCESS_TABLE + currentPID*16 + 9] = 0;
    ireturn;
  endif;

  alias fileIndex R8;
  fileIndex = [userAreaPageNum*512 + 496 + 2*fileDescriptor + 1];

  alias inodeIndex R9;
  inodeIndex = [OPEN_FILE_TABLE + fileIndex*4];

  if ([INODE_TABLE + inodeIndex*16 + 3] != [PROCESS_TABLE + currentPID*16 + 3]
    && [INODE_TABLE + inodeIndex*16 + 3] != 1 && [INODE_TABLE + inodeIndex*16 + 4] == EXCLUSIVE) then
    [physicalAddrRetVal] = -3;

    backup;
    R1 = RELEASE_LOCK;
    R2 = KERN_LOCK;
    call ACCESS_CONTROL;
    restore;

    SP = userSP;
    [PROCESS_TABLE + currentPID*16 + 9] = 0;
    ireturn;
  endif;

  multipush(R5, R6, R7, R8, R9, R14, R15);
  R1 = ACQUIRE_INODE;
  R2 = inodeIndex;
  R3 = currentPID;
  call RESOURCE_MANAGER;
  multipop(R5, R6, R7, R8, R9, R14, R15);

  if (R0 == -1) then
    [physicalAddrRetVal] = -1;

    backup;
    R1 = RELEASE_LOCK;
    R2 = KERN_LOCK;
    call ACCESS_CONTROL;
    restore;

    SP = userSP;
    [PROCESS_TABLE + currentPID*16 + 9] = 0;
    ireturn;
  endif;

  alias Lseek R10;
  Lseek = [OPEN_FILE_TABLE + fileIndex*4 + 2];

  if (Lseek == PAGE_SIZE*4) then
    backup;
    R1 = RELEASE_INODE;
    R2 = inodeIndex;
    R3 = currentPID;
    call RESOURCE_MANAGER;
    restore;

    [physicalAddrRetVal] = -2;

    backup;
    R1 = RELEASE_LOCK;
    R2 = KERN_LOCK;
    call ACCESS_CONTROL;
    restore;

    SP = userSP;
    [PROCESS_TABLE + currentPID*16 + 9] = 0;
    ireturn;
  endif;

  if (Lseek%512 == 0 && Lseek == [INODE_TABLE + inodeIndex*16 + 2]) then
    alias block R11;

    multipush(R5, R6, R7, R8, R9, R10, R14, R15);
    R1 = GET_FREE_BLOCK;
    call MEMORY_MANAGER;
    multipop(R5, R6, R7, R8, R9, R10, R14, R15);

    if (block == -1) then
      backup;
      R1 = RELEASE_INODE;
      R2 = inodeIndex;
      R3 = currentPID;
      call RESOURCE_MANAGER;
      restore;

      [physicalAddrRetVal] = -2;

      backup;
      R1 = RELEASE_LOCK;
      R2 = KERN_LOCK;
      call ACCESS_CONTROL;
      restore;

      SP = userSP;
      [PROCESS_TABLE + currentPID*16 + 9] = 0;
      ireturn;
    endif;

    [INODE_TABLE + inodeIndex*16 + Lseek/512 + 8] = R0;
  endif;

  alias word R11;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  backup;
  R1 = BUFFERED_WRITE;
  R2 = [INODE_TABLE + inodeIndex*16 + Lseek/512 + 8];
  R3 = Lseek%512;
  R4 = word;
  call FILE_MANAGER;
  restore;

  if (Lseek == [INODE_TABLE + inodeIndex*16 + 2]) then
    [INODE_TABLE + inodeIndex*16 + 2] = Lseek + 1;
    [ROOT_FILE + inodeIndex*8 + 1] = Lseek + 1;
  endif;

  [OPEN_FILE_TABLE + fileIndex*4 + 2] = Lseek + 1;

  backup;
  R1 = RELEASE_INODE;
  R2 = inodeIndex;
  R3 = currentPID;
  call RESOURCE_MANAGER;
  restore;
else
  alias word R7;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  backup;
  R1 = TERMINAL_WRITE;
  R2 = currentPID;
  R3 = word;
  call DEVICE_MANAGER;
  restore;
endif;

backup;
R1 = RELEASE_LOCK;
R2 = KERN_LOCK;
call ACCESS_CONTROL;
restore;

[physicalAddrRetVal] = 0;

SP = userSP;
[PROCESS_TABLE + currentPID*16 + 9] = 0;
ireturn;
